---
title: Construye una piscina de lotería sin pérdidas
description: Aprende cómo crear un fondo de lotería sin pérdidas que aprovecha el rendimiento del stacking.
---
import { Code, Terminal } from 'lucide-react';

Un contrato de lotería sin pérdidas ofrece una forma única para que los participantes acumulen sus activos y potencialmente ganen una recompensa mayor sin el riesgo de perder su depósito inicial.

Este contrato garantiza que los participantes puedan apilar sus activos en un fondo generador de rendimientos, recibir un boleto NFT y tener la oportunidad de ganar recompensas adicionales mientras conservan su inversión original.

En esta guía, aprenderás cómo:

1. Definir constantes y variables de datos
2. Crear y gestionar participantes y entradas
3. Implementar la funcionalidad de la lotería
4. Manejar la selección de ganadores
5. Reclamar y distribuir recompensas

:::callout
### Nota

Este ejemplo utiliza el protocolo CityCoins para el mecanismo de rendimiento de stacking, pero también se puede utilizar un pool de Stacking usando Proof of Transfer (PoX4).
:::

***

## Definir constantes y variables de datos

Primero, define algunas constantes y variables de datos para gestionar el estado de tu contrato. Las constantes se utilizan para valores fijos, y las variables de datos almacenan el estado que puede cambiar durante la ejecución del contrato.

```clarity
(define-constant OWNER tx-sender)
(define-constant ERR_UNAUTHORIZED (err u101000))

(define-data-var lotteryPool uint u0)
(define-data-var totalYield uint u0)
(define-data-var ticketCounter uint u0)
```

El `OWNER` constant define al propietario del contrato con privilegios administrativos, mientras que `ERR_UNAUTHORIZED` maneja intentos de acceso no autorizados.

Las variables de datos clave incluyen `lotteryPool` para el seguimiento de activos apilados, `totalYield` para ganancias acumuladas, y `ticketCounter` para gestionar los tickets emitidos.

## Crear y gestionar participantes y entradas

A continuación, define un NFT para representar un boleto y un mapa para almacenar la información de los participantes.

```clarity
(define-map Participants
  { participant: principal }
  { ticketId: uint, amount: uint, cycle: uint, ticketExpirationAtCycle: uint, isWinner: bool }
)

(define-map Tickets
  { ticketId: uint }
  { owner: principal }
)

(define-non-fungible-token LotteryTicket uint)
```

El `Participants` map vincula a los participantes con los detalles de sus boletos, mientras que el `Tickets` el mapa asocia los boletos con sus propietarios.

El `LotteryTicket` define un NFT para los boletos de lotería, crucial para gestionar las entradas.

## Implementar la funcionalidad de la lotería

Ahora, es momento de implementar la función principal del contrato, donde los participantes pueden entrar en la lotería depositando sus activos.

```clarity
(define-public (roll-the-dice (cityName (string-ascii 10)) (amount uint) (lockPeriod (optional uint)))
  (let
    (
      (ticketId (+ (var-get ticketCounter) u1))
      (actualLockPeriod (default-to u1 lockPeriod))
      (rewardCycle (+ (contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd007-citycoin-stacking get-current-reward-cycle) u1))
    )

    (begin
      (try! (contract-call? 'SP1H1733V5MZ3SZ9XRW9FKYGEZT0JDGEB8Y634C7R.miamicoin-token-v2 transfer amount tx-sender (as-contract tx-sender) none))
      (try! (nft-mint? LotteryTicket ticketId tx-sender))
      (map-insert Participants { participant: tx-sender } { ticketId: ticketId, amount: amount, cycle: rewardCycle, ticketExpirationAtCycle: (+ rewardCycle actualLockPeriod), isWinner: false })
      (map-set Tickets { ticketId: ticketId } { owner: tx-sender })
      (var-set lotteryPool (+ (var-get lotteryPool) amount))
      (var-set ticketCounter (+ (var-get ticketCounter) u1))
      (ok ticketId)
    )
  )
)
```

El `roll-the-dice` function permite a los participantes unirse a la lotería depositando activos, especificando la ciudad (`cityName`), el monto del depósito (`amount`), y el período de bloqueo (`lockPeriod`).

Esta función es crucial para gestionar las participaciones en la lotería y garantizar que los activos estén correctamente bloqueados durante el período especificado.

Cuando un participante llama a esta función, ocurren los siguientes pasos:

1. **Generar ticket y determinar período de bloqueo**: Se genera un nuevo ID de ticket y se establece el período de bloqueo (por defecto es 1 si no se especifica).
2. **Transferir activos y acuñar boleto NFT**: La cantidad especificada de activos se transfiere del participante al contrato, y se acuña un NFT que representa el boleto de lotería y se asigna al participante.
3. **Actualizar datos de participantes y boletos**: La información del participante, incluyendo el ID del boleto, la cantidad, el ciclo y la fecha de vencimiento, se almacena, y se actualiza la información de propiedad del boleto.
4. **Actualizar el fondo de lotería y devolver el ID del boleto**: La cantidad total en el pozo de la lotería se actualiza, el contador de boletos se incrementa y la función devuelve el ID del boleto recién generado al participante.

Este proceso simplificado garantiza que la participación de cada concursante se registre correctamente y que sus activos se gestionen de forma segura dentro del sistema de lotería.

## Manejo de la selección de ganadores

Ahora, implementa la función para seleccionar un ganador aleatoriamente de entre los participantes.

```clarity
(define-private (select-winner)
  (match (contract-call? 'SPSCWDV3RKV5ZRN1FQD84YE1NQFEDJ9R1F4DYQ11.citycoin-vrf-v2 get-save-rnd (- block-height u1)) randomTicketNumber
    (let
      (
        (ticketCount (var-get ticketCounter))
        (winningTicketId (mod randomTicketNumber ticketCount))
        (winner (unwrap! (get-ticket winningTicketId) (err u404)))
        (owner (get owner winner))
        (participant (unwrap! (get-participant owner) (err u404)))
      )
      (map-set Participants { participant: owner }
        { ticketId: winningTicketId, amount: (get amount participant), cycle: (get cycle participant), ticketExpirationAtCycle: (get ticketExpirationAtCycle participant), isWinner: true })
      (ok owner)
    )
    selectionError (err selectionError)
  )
)
```

El `select-winner` function selecciona aleatoriamente un ganador utilizando un número del contrato VRF (`get-save-rnd`) y actualiza su estado con `unwrap!`. Esto garantiza un proceso justo y transparente de selección del ganador.

Cuando se llama a esta función, ocurren los siguientes pasos:

1. **Obtener número aleatorio**: Se obtiene un número aleatorio del contrato VRF mediante una llamada `get-save-rnd` con la altura del bloque anterior.
2. **Determinar el boleto ganador**: El ID del boleto ganador se calcula tomando el módulo del número aleatorio con el número total de boletos (`ticketCounter`).
3. **Recuperar información del ganador**: Se recuperan el boleto ganador y la información del participante utilizando el ID de boleto calculado.
4. **Actualizar estado del ganador**: El estado del participante se actualiza para indicar que es un ganador estableciendo `isWinner` a `true` en el `Participants` mapa.

Este proceso garantiza que el ganador sea seleccionado de manera justa y que su estado se actualice con precisión en el sistema.

## Reclamar y distribuir recompensas

Por último, implementa la función para reclamar y distribuir recompensas a los ganadores.

```clarity
(define-public (claim-rewards (cityName (string-ascii 10)) (cycle uint))
  (let
    (
      (cityId (unwrap-panic (contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd004-city-registry get-city-id cityName)))
      (cycleAmount (contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd002-treasury-mia-stacking get-balance-stx))
    )
    (if (is-eq cityName "mia")
      (try! (contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd011-stacking-payouts send-stacking-reward-mia cycle cycleAmount))
      (try! (contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd011-stacking-payouts send-stacking-reward-nyc cycle cycleAmount))
    )
    (as-contract (contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd007-citycoin-stacking claim-stacking-reward cityName cycle))
  )
)

(define-public (distribute-rewards)
  (select-winner)
)
```

El `claim-rewards` function permite a los participantes reclamar sus recompensas de staking, mientras que la `distribute-rewards` llamadas a funciones `select-winner` para seleccionar y recompensar aleatoriamente a los ganadores, asegurando un proceso de distribución justo.

Cuando el `claim-rewards` La función es llamada, ocurren los siguientes pasos:

1. **Obtener ID de ciudad y saldo**: El ID de la ciudad se obtiene utilizando el `get-city-id` función, y el saldo para el ciclo especificado se obtiene del contrato de tesorería.
2. **Enviar recompensas de staking**: Dependiendo de la ciudad (`mia` o `nyc`), se llama a la función de recompensa de staking apropiada para enviar las recompensas del ciclo especificado.
3. **Reclamar recompensa acumulada**: El contrato reclama la recompensa de stacking para la ciudad y ciclo especificados.

Cuando el `distribute-rewards` La función es llamada, realiza el siguiente paso:

1. **Seleccionar ganador**: El `select-winner` Se llama a la función para seleccionar aleatoriamente un ganador entre los participantes y actualizar su estado.

Este proceso garantiza que los participantes puedan reclamar sus recompensas y que los ganadores sean seleccionados y premiados de manera justa.

## Probando el contrato

Para probar los contratos, siga los siguientes pasos dentro de `clarinet console`:

```terminal
$ ::advance_chain_tip 700000
$ ::set_tx_sender
$ (contract-call? 'ST1PQHQKV0RJXZFY1DGX8MNSNYVE3VGZJSRTPGZGM.no-loss-lottery-pool roll-the-dice "mia" u500 none)
$ ::advance_chain_tip 2000
$ (contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd002-treasury-mia-stacking deposit-stx u5000000000)
$ (contract-call? 'ST1PQHQKV0RJXZFY1DGX8MNSNYVE3VGZJSRTPGZGM.no-loss-lottery-pool claim "mia" u17)
```

Para iniciar los contratos de CityCoins, sigue estos pasos:

```clarity
(contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd004-city-registry get-or-create-city-id "mia")
(contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd004-city-registry get-or-create-city-id "nyc")
(contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd002-treasury-mia-stacking set-allowed 'SP1H1733V5MZ3SZ9XRW9FKYGEZT0JDGEB8Y634C7R.miamicoin-token-v2 true)
(contract-call? 'SP8A9HZ3PKST0S42VM9523Z9NV42SZ026V4K39WH.ccd002-treasury-nyc-stacking set-allowed 'SPSCWDV3RKV5ZRN1FQD84YE1NQFEDJ9R1F4DYQ11.newyorkcitycoin-token-v2 true)
```
